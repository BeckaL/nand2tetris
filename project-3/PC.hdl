// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in = secondOut, out = outPlusOne);
    Mux16(a = secondOut, b = outPlusOne, sel = inc, out = incrementedOut);
    Mux16(a = incrementedOut, b = in, sel = load, out = loadedIn);
    Mux16(a = loadedIn, b[0..15] = false, sel = reset, out = finalOut);
    Or(a = load, b = inc, out = loadOrInc);
    Or(a = loadOrInc, b = reset, out = changeValue);
    Register(in = finalOut, load = changeValue, out = out, out[0..15] = secondOut);
}
